#!/usr/bin/env perl

# Copyright 2001-2017, Paul Johnson (paul@pjcj.net)

# This software is free.  It is licensed under the same terms as Perl itself.

# The latest version of this software should be available from my homepage:
# http://www.pjcj.net

use strict;
use warnings;

my $Command = {
    set_version => sub {
        my ($command, $version, $date, @files) = @_;
        local ($^I, @ARGV) = ("", @files);
        while (<>) {
            s/(^\s*(?:our\s+)?\$VERSION = ")\d+\.\d+(";)/$1$version$2/;
            s/(Version )\d+\.\d+( - ).*/$1$version$2$date/;
            s/(^\s*use Gedcom(?:::\w+)*\s+)\d+\.\d+;/$1$version;/;
            print;
        }
    },
    make_readme => sub {
        my ($command) = @_;
        local @ARGV;
        while (<>) {
            print if (/NAME/        ... /^[A-Z ]+$/) =~ /^\d+$/;
            print if (/DESCRIPTION/ ... /^[A-Z ]+$/) =~ /^\d+$/;
        }
    },
    munge_readme => sub {
        my ($command) = @_;
        local @ARGV;
        while (<>) {
            next if $. < 3;
            s/^/# / if $. == 3;
            if ($. == 5) {
                my $coveralls = "https://coveralls.io/repos/github/" .
                                "pjcj/Gedcom.pm/badge.svg?branch=master";
                my $travis    = "https://travis-ci.org/" .
                                "pjcj/Gedcom.pm.svg?branch=master";
                my $badges    = "[![Build Status]($travis)]($travis)" .
                                "[![Coverage Status]($coveralls)]($coveralls)";
                s/.*/$badges/;
            };
            print;
        }
    },
};

sub main {
    my ($command) = @ARGV;
    die "No such command: $command" unless $Command->{$command};
    $Command->{$command}->(@ARGV)
}

main
